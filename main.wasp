app TodoApp {
  wasp: {
    version: "^0.11.1"
  },
  title: "TodoApp",
  client: {
    rootComponent: import { Layout } from "@client/Layout",
  },

  dependencies: [
    ("sass", "1.64.1"),
    ("@tailwindcss/forms", "0.5.4"),
    ("@tailwindcss/typography", "0.5.9"),
  ],

  db: {
    system: SQLite,
    prisma: {
      clientPreviewFeatures: ["extendedWhereUnique"]
    }
  },

  auth: {
    userEntity: User,
    methods: {
      usernameAndPassword: {}
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/"
  },
}

////////////////////////////////////////////
// Prisma Schema
////////////////////////////////////////////

entity User {=psl
    id       Int    @id @default(autoincrement())
    username String @unique
    password String
    tasks    Task[]
psl=}

entity Task {=psl
    id          Int     @id @default(autoincrement())
    description String
    isDone      Boolean @default(false)
    user        User    @relation(fields: [userId], references: [id])
    userId      Int
psl=}

////////////////////////////////////////////
// Queries
////////////////////////////////////////////

query getTasks {
  fn: import { getTasks } from "@server/queries/getTasks.js",
  entities: [Task]
}

////////////////////////////////////////////
// Actions
////////////////////////////////////////////

action createTask {
  fn: import { createTask } from "@server/actions/createTask.js",
  entities: [Task]
}

action completeTask {
  fn: import { completeTask } from "@server/actions/completeTask.js",
  entities: [Task]
}

action updateTask {
  fn: import { updateTask } from "@server/actions/updateTask.js",
  entities: [Task]
}

////////////////////////////////////////////
// Routes
////////////////////////////////////////////

route DashboardRoute { path: "/", to: DashboardPage }
route LoginRoute { path: "/login", to: LoginPage }
route SignupRoute { path: "/signup", to: SignupPage }

page DashboardPage {
  component: import Dashboard from "@client/pages/DashboardPage",
  authRequired: true
}

page LoginPage {
  component: import Login from "@client/pages/LoginPage"
}

page SignupPage {
  component: import Signup from "@client/pages/SignupPage"
}
